plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    implementation(include("io.github.llamalad7:mixinextras-forge:0.4.1"))
    modRuntimeOnly "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"
    modRuntimeOnly "curse.maven:chiselreborn-551763:$project.chisel_reborn_version"
    modImplementation "curse.maven:continuity-531351:$project.continuity_version"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    group = "publishing"
    apiToken = System.getenv('CURSEFORGE_API_KEY') ?: 'XXX'

    def mainFile = upload(rootProject.property('curseforge_id'), remapJar)
    mainFile.releaseType = project.version_type
    mainFile.changelog = changelog.text
    mainFile.changelogType = 'markdown'
    mainFile.displayName = "${rootProject.name} ${project.version}"
    mainFile.addGameVersion('1.20.1')
    mainFile.addJavaVersion('Java 17')
    mainFile.addRequirement('continuity')
    mainFile.addOptional('chisel-reborn')
    mainFile.addModLoader(project.name)
    mainFile.withAdditionalFile(remapSourcesJar)
}

def changelogText = changelog.text

modrinth {
    var modrinthToken = System.getenv('MODRINTH_TOKEN') ?: 'XXX'

    token = modrinthToken
    // Remember to have the MODRINTH_TOKEN environment variable set or else this will fail - just make sure it stays private!
    projectId = rootProject.property('modrinth_id') // This can be the project ID or the slug. Either will work!
    versionNumber = project.version as String
    versionName = "${rootProject.name} ${project.version}"
    // You don't need to set this manually. Will fail if Modrinth has this version already
    versionType = project.version_type // This is the default -- can also be `beta` or `alpha`
    uploadFile = jar // With Loom, this MUST be set to `remapJar` instead of `jar`!
    additionalFiles = [sourcesJar]
    gameVersions = [project.minecraft_version] // Must be an array, even with only one version
    loaders = [project.name] // Must also be an array - no need to specify this if you're using Loom or ForgeGradle
    changelog = changelogText
    dependencies {
        required.project 'continuity'
        optional.project 'chisel-reborn'
    }
}
